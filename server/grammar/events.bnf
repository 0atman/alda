<part>                = <ows> events <ows>
<events>              = event? (<ows> event)*
<events-inside-voice> = event-inside-voice? (<ows> event-inside-voice)*

    (* notes, chords & other events *)

<event>               = single-event | repeat | voices
<event-inside-voice>  = single-event | repeat
<single-event>        = chord | note | rest | octave-change |
                        clj-expr-cached | marker | at-marker | barline |
                        event-sequence | cram

repeat                = single-event <ows> <"*"> <ows> positive-number <ows>

    (* chords, notes, rests *)

chord                 = (note | rest) subchord+
<subchord>            = <ows> inside-chord*
                        <ows> <"/">
                        <ows> inside-chord*
                        <ows> (note | rest) <ows>
<inside-chord>        = octave-change | clj-expr-cached
note                  = pitch duration? <ows> slur?
rest                  = <"r"> duration? <ows>

    (* pitch *)

pitch                 = #"[a-g]" accidental*
<accidental>          = flat | sharp | natural
flat                  = "-"
sharp                 = "+"
natural               = "="

    (* octaves *)

<octave-change>       = (octave-set | octave-up | octave-down) <ows>
octave-set            = <"o"> number
octave-up             = <">">
octave-down           = <"<">

    (* markers *)

marker                = <"%"> name <ows>
at-marker             = <"@"> name <ows>

