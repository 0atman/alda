score                   = <ows> instrument+
instrument              = instrument-call <ows> music-data
instrument-call         = name
                          (<ows> <"/"> <ows> name)*
                          (<ows> nickname)?
                          <ows> <":">
<acceptable-name>       = #"[a-zA-Z]{2}[\w\-'()]*"
name                    = acceptable-name
nickname                = <"\""> acceptable-name <"\"">

music-data              = (voices | event)*
<event>                 = chord | note | rest | octave-change |
                          attribute-change | marker | at-marker
voices                  = voice+ (<voice-zero> | <#"\z">)
voice                   = voice-number event*
voice-number            = <"V"> #"[1-9]\d*" <":"> <ows>
<voice-zero>            = <"V0:"> <ows>

chord                   = (note | rest) subchord+
<subchord>              = <"/"> <ows> (octave-change | attribute-change)*
                                <ows> (note | rest) <ows>
note                    = pitch duration? <ows>
rest                    = "r" duration? <ows>

pitch                   = !name #"[a-g][+-]*"
duration                = note-length <ows> (tie <ows> note-length <ows>)*
<note-length>           = number dots?
<number>                = #"-?[0-9]+"
<dots>                  = #"\.+"
<tie>                   = "~"

octave-change           = (<"o"> number | ">" | "<") <ows>
attribute-change        = "(" <ows>
                          (attribute <ows> number <ows> #"[,;]" <ows>)+
                          ")" <ows>
attribute               = "volume" | "pan" | "tempo"

marker                  = <"%"> name <ows>
at-marker               = <"@"> name <ows>

ows                     = (#"\s" | comment)*

comment                 = long-comment | short-comment | barline
long-comment            = "#{" inside-long-comment* "}"
inside-long-comment     = !("#{" | "}") #".|\n|\r" | long-comment
short-comment           = "#" #".*" #"(\n|\r|$)+"
barline                 = "|"
